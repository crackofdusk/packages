pkgname=dwm-git
_pkgname=dwm
pkgver=6.1.22.g3756f7f
pkgver(){
  cd $_pkgname
  git describe --tags |sed 's/-/./g'
}
pkgrel=1

pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
epoch=1
source=( dwm.desktop
    config.h
    dwm-6.1-pwkl.patch
    dwm-status-padding-20171201.patch
    "$_pkgname::git+http://git.suckless.org/dwm")

prepare() {
  if [[ -f $srcdir/config.h ]]; then
    ln -sf $srcdir/config.h $srcdir/$_pkgname/config.h
  fi

  cd $_pkgname
  # custom patches
  for i in $srcdir/*\.patch
  do
    msg "patching with $i"
    patch -i $i
  done

}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  make -C $_pkgname PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D $_pkgname/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D $_pkgname/README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}

sha256sums=('bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            '2e0745a7ba9fc4a6bc1720c53392108c764d481dbb1c9e518045de81ed306aef'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'c6c3af22ab45c3da381917e146b913f86a63fa2b2c1f39292d9077256d5b18ee'
            'SKIP')
