# Maintainer:  Håvard Pettersson <mail@haavard.me>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

_pkgname=dmenu
pkgname=$_pkgname-git
pkgver=4.6.0.g32f2564
pkgrel=1
pkgdesc="A generic menu for X"
url="http://tools.suckless.org/dmenu/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://git.suckless.org/$_pkgname
        config.h
        dmenu-git-padding.patch)
sha256sums=('SKIP'
            '9dc91e2f8ff64646174fe8751dd2030df66cbe89366c9e0ed2f0082e0a488d2a'
            '30b88d35c4d356f95b58bca3d42b936a0fdc3521f16c7388545ca7f8c4ee68e4')

pkgver() {
  cd $_pkgname
  git describe --tags --long | sed 's/-/./g'
}

prepare() {
  if [[ -f $srcdir/config.h ]]; then
    ln -sf $srcdir/config.h $srcdir/$_pkgname/config.h
  fi

  cd $_pkgname
  # custom patches
  for i in $srcdir/*\.patch
  do
    msg "patching with $i"
    patch -i $i
  done
}

build(){
  cd $_pkgname
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
