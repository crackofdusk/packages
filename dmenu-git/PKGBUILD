# Maintainer:  Håvard Pettersson <mail@haavard.me>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

_pkgname=dmenu
pkgname=$_pkgname-git
pkgver=4.8.12.gbbc464d
pkgrel=1
pkgdesc="A generic menu for X"
url="http://tools.suckless.org/dmenu/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://git.suckless.org/$_pkgname
        config.h
        dmenu-padding-20171201.patch)

pkgver() {
  cd $_pkgname
  git describe --tags --long | sed 's/-/./g'
}

prepare() {
  if [[ -f $srcdir/config.h ]]; then
    ln -sf $srcdir/config.h $srcdir/$_pkgname/config.h
  fi

  cd $_pkgname
  # custom patches
  for i in $srcdir/*\.patch
  do
    msg "patching with $i"
    patch -i $i
  done
}

build(){
  cd $_pkgname
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
sha256sums=('SKIP'
            '59ee712b7580e9a8747fc61bcfa2af25b9948c5440f768c168067daf74acfd5b'
            'f1ef4b5c97540c3a9293134bf35439838d40cd2bd8ef49675c6127c0efe1128c')
